plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common
}

dependencies {
    neoForge "net.neoforged:neoforge:${neoforge_version}"
    modApi "dev.architectury:architectury-neoforge:${architectury_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionNeoForge")) { transitive = false }

    modApi "maven.modrinth:bocchud:0.1.2-mc1.20.4" // 0.1.2-mc1.20.4
    modApi 'curse.maven:ftb-library-404465:5205161' // neoforge-2004.2.1
    modApi 'curse.maven:ftb-ultimine-386134:5204156' // neoforge-2004.1.0
    modApi 'maven.modrinth:mafglib:0.1.8-mc1.20.4' // 0.1.8-mc1.20.4
    modApi 'maven.modrinth:forged-networking-api:0.1.2+mc1.20.4'
    modApi 'curse.maven:mekanism-268560:5257006' // 1.20.4-10.5.19.40
    modApi 'curse.maven:mekanism-generators-268566:5257009' // 1.20.4-10.5.19.40
    modApi 'curse.maven:glitchcore-955399:5088442' // NeoForge 1.20.4-1.0.0.59
    modApi 'curse.maven:serene-seasons-291874:5206533' // 1.20.4-9.3.0.25
//    modApi 'curse.maven:natures-aura-306626:5220071' // 40.1 // 映射还是有问题
    modApi files('libs/NaturesAura-40.1-api.jar')
}

processResources {
    var replaceProperties = [
            neoforge_version: neoforge_version, neoforge_version_range: neoforge_version_range,
            neoforge_loader_version_range: neoforge_loader_version_range,
            mod_id: mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
            mod_authors: mod_authors, mod_description: mod_description, mod_repo: mod_repo,
            architectury_dependency: architectury_dependency, bocchud_dependency: bocchud_dependency
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml']) {
        expand replaceProperties + [project: project]
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = archives_base_name + "-" + project.name
            from components.java
        }
    }

    repositories {
    }
}